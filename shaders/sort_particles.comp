#version 460 core

layout(local_size_x = 1024) in;

layout(std430, binding = 0) coherent readonly buffer block0
{
    uint in_prefixSums[];
};

layout(std430, binding = 1) coherent buffer block1
{
    uint inout_particlesCells[];
};

layout(std430, binding = 2) readonly buffer block2
{
    vec4 in_predictedPositions[];
};

layout(std430, binding = 3) coherent writeonly buffer block3
{
    vec4 out_predictedPositions[];
};

layout(std430, binding = 4) readonly buffer block4
{
    vec4 in_origPositions[];
};

layout(std430, binding = 5) coherent writeonly buffer block5
{
    vec4 out_origPositions[];
};

struct Boundary
{
    vec3 low;
    vec3 high;
};

uniform Boundary u_boundary;
uniform uvec3 u_gridResolution;

uint cellID(vec3 position)
{
    const vec3 diagonal = u_boundary.high - u_boundary.low;
    vec3 cellSize = diagonal / u_gridResolution;
    uvec3 cellIdx = uvec3((position - u_boundary.low) / cellSize);
    uint idx =  cellIdx.x + u_gridResolution.x * cellIdx.y + u_gridResolution.x * u_gridResolution.y * cellIdx.z;
    return idx;
}

void main()
{
    uint id = gl_GlobalInvocationID.x;
    vec4 position = in_predictedPositions[id];
    uint cellIdx = cellID(position.xyz);
    uint particleIdxCell = atomicAdd(inout_particlesCells[cellIdx], -1);
    uint particleIdx = in_prefixSums[cellIdx] - particleIdxCell;
    out_predictedPositions[particleIdx] = position;
    out_origPositions[particleIdx] = in_origPositions[id];
}
